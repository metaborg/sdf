module sdf

imports
  libstratego-lib
  libstratego-sdf
  libstratego-sglr
  libstrc
  analysis
  generate-syntax-templates
  editor-common.generated
  include/SDF
  completion
  check
  editor/SDF-Outliner
  editor/-
  //names

rules

  editor-analyze:
    (ast, path, project-path) -> (error'*, warning'*, [])
    with
      editor-init;
      analyze-ast;
      // We only collect-om since we don't want inner warning dupes
      error*   := <collect-om(fetch-sdf-errors, conc)> ast;
      warning* := <collect-om(fetch-sdf-warnings, conc)> ast;
      if <file-exists> $[[project-path]/.disable-global-analysis] then
        serror* := []
      else
        serror* := <collect-om(context-free-syntax-sort-error, conc)> ast
      end;
      if IsImportFailed then
        error'*   := error*;
        warning'* := <conc> (serror*, warning*)
      else
        error'*   := <conc> (serror*, error*);
        warning'* := warning*
      end


  editor-resolve:
    (sort(x), position, ast, path, project-path) -> target
    where
      target := <Declaration> x

  editor-resolve:
    (unparameterized(x), position, ast, path, project-path) -> target
    where
      target := <parse-sdf-file> <resolve-import-decl> x

  override pp-sdf-string = fail // ensure compiler doesn't blow up


rules

  generate-pp-rules:
    (selected, position, ast, path, project-path) -> (filename, result)
    where
      filename  := <guarantee-extension(|"selection.pp")> path;
      selected' := <add-context; sdf-desugar> selected;
      result    := <ppgenerate; pp-pp-table> selected'
    <+
      fatal-err-msg(|$[Could not generate pretty printer rules for this selection.])

  generate-rtg:
    (selected, position, ast, path, project-path) -> (filename, result)
    where
      filename  := <guarantee-extension(|"rtg")> path;
      selected' := <debug(!2); add-context; debug(!3); sdf-desugar> selected;
      result    := <debug(!4); core-rtg2sig; debug(!5); pp-stratego-string> selected'
    <+
      fatal-err-msg(|$[Could not generate abstract syntax signatures for this selection.])

  generate-aterm:
    (selected, position, ast, path, project-path) -> (filename, selected)
    where
      filename  := <guarantee-extension(|"aterm")> path

  add-context =
    if not(oncetd(?context-free-syntax(_))) then
      if is-list then
        !context-free-syntax(<id>)
      else
        !context-free-syntax([<id>])
      end
      // !'module(unparameterized("Module"), [], [exports(<id>)])
    end

rules // External tools not included in the standard libraries

  ppgenerate =
    <call> ("org.strategoxt.tools.ppgenerate", [<id>])

  core-rtg2sig =
    <call> ("org.strategoxt.tools.core-rtg2sig", [<id>])

  pp-pp-table =
    <call> ("org.strategoxt.tools.pp-pp-table", [<id>])

  sdf-desugar =
    <call> ("org.strategoxt.tools.sdf-desugar", [<id>]) => [_, <id>]
