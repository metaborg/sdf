module generate-signatures

imports
  libstratego-lib
  libstrc
  analysis/types
  runtime/nabl/-
  runtime/task/-
  runtime/types/-
  signatures/statix/-
  pp/statix/-
  pp/statix/lang/-

imports
  injections-common
  injections-utils

imports
  signatures/aliases/-
  signatures/aterms/-
  signatures/basic/-
  signatures/characterclass/-
  signatures/constants/-
  signatures/grammar/-
  signatures/kernel/-
  signatures/labels/-
  signatures/layout/-
  signatures/layout-constraints/-
  signatures/lifting/-
  signatures/literals/-
  signatures/modules/-
  signatures/priority/-
  signatures/regular/-
  signatures/renaming/-
  signatures/restrictions/-
  signatures/sdf2-core/-
  signatures/sorts/-
  signatures/symbols/-
  signatures/TemplateLang-sig

rules

  // geninj-generate-signatures :: SDF3.Module -> Statix.Module
  geninj-generate-signatures = strip-annos; geninj-module-to-sig


  // geninj-module-to-sig :: SDF3.Module -> Statix.Module
  geninj-module-to-sig:
    Module(modname, imports, sections) -> Module(modname', [
      Imports(imports'),
      Signature([
        Sorts(<concatenate> (stx-lexsorts, stx-cfsorts)),
        Constructors(stx-plhdr-sorts)
      ]),
      Signature([Constructors(stx-sigs)])
    ])
  with modname'         := $[signatures/[<?Unparameterized(<id>)> modname]-sig]
     ; imports'         := <flatmap(geninj-imports-to-stx-imports(|"-sig", "signatures"))> imports
     // Collect CF and LEX sorts
     ; allsorts         := <geninj-sections-to-sorts; debug(!"All sorts: ")> sections
     ; cfsorts          := <filter(?CfSort(_)); debug(!"CF sorts: ")>  allsorts
     ; lexsorts         := <filter(?LexSort(_)); debug(!"LEX sorts: ")> allsorts
     // Collect signatures
     ; allsigs          := <geninj-sections-to-signatures; debug(!"All sigs: ")> sections
     ; injsigs          := <filter(?CfSignature(_, None(), _)); debug(!"Inj sigs: ")> allsigs
     ; conssigs         := <filter(?CfSignature(_, Some(_), _)); debug(!"Cons sigs: ")> allsigs
     // Statix sorts
     ; stx-lexsorts     := <map(geninj-lexsort-to-stxsort)> lexsorts
     ; stx-cfsorts      := <map(geninj-cfsort-to-stxsort)> cfsorts
     ; stx-plhdr-sorts  := <map(geninj-sort-to-stxsig)> cfsorts
     // Statix signatures
     ; stx-sigs         := <filter(geninj-sig-to-stxsig); flatten-list-once> injsigs

rules

  // stx-to-str-import :: SDF3.ImpSection -> [Statix.Import]
  geninj-imports-to-stx-imports(|ext, folder):
    Imports(imports) -> <map(!Import(<geninj-modulename-to-stx-importname(|ext, folder)>))> imports


  // stx-to-module-name(|string, string) :: SDF3.Import -> string
  geninj-modulename-to-stx-importname(|ext, folder):
    Module(Unparameterized(m)) -> <conc-strings> (folder, "/" , m, ext)


rules

  // geninj-lexsort-to-stxsort :: ExtSort -> Statix.SortDecl
  geninj-lexsort-to-stxsort: LexSort(sortname) ->
    SortAlias(sortname, StringSort())


  // geninj-cfsort-to-stxsort :: ExtSort -> Statix.SortDecl
  geninj-cfsort-to-stxsort: CfSort(sortname) ->
    SortDecl(sortname)

rules

  geninj-sig-to-stxsig: CfSignature(sortname, None(), params) ->
    OpDecl(consname, op)
  with consname := <geninj-generate-inj-name> (sortname, params)
     ; stx-paramsigs := <map(geninj-param-to-stxsig)> params
     ; op := <geninj-stxparamsigs-to-op(|sortname)> stx-paramsigs


  geninj-sig-to-stxsig: CfSignature(sortname, Some(consname), params) -> OpDecl(consname, op)
  with stx-paramsigs := <map(geninj-param-to-stxsig)> params
     ; op := <geninj-stxparamsigs-to-op(|sortname)> stx-paramsigs

  geninj-sort-to-stxsig: CfSort(sortname)  -> OpDecl($[[sortname]-Plhdr], ConstOp(SimpleSort(sortname)))
  geninj-sort-to-stxsig: LexSort(sortname) -> OpDecl($[[sortname]-Plhdr], ConstOp(SimpleSort(sortname)))
  geninj-sort-to-stxsig: VarSort(sortname) -> OpDecl($[[sortname]-Plhdr], ConstOp(SimpleSort(sortname)))

rules

  geninj-stxparamsigs-to-op(|sortname): [] -> ConstOp(SimpleSort(sortname))
  geninj-stxparamsigs-to-op(|sortname): sigs@[_|_] -> ArrowOp(sigs, SimpleSort(sortname))

  geninj-param-to-stxsig: Param(symbol, _) -> <geninj-symbol-to-stxsig> symbol

  // geninj-symbol-to-stxsig :: SDF3.Symbol     -> Statix.SortRef
  geninj-symbol-to-stxsig: Sort(name)           -> SimpleSort(name)
  geninj-symbol-to-stxsig: Opt(sort)            -> <geninj-symbol-to-stxsig> sort
  geninj-symbol-to-stxsig: Iter(sort)           -> ListSort(<geninj-symbol-to-stxsig> sort)
  geninj-symbol-to-stxsig: IterSep(sort, _)     -> ListSort(<geninj-symbol-to-stxsig> sort)
  geninj-symbol-to-stxsig: IterStar(sort)       -> ListSort(<geninj-symbol-to-stxsig> sort)
  geninj-symbol-to-stxsig: IterStarSep(sort, _) -> ListSort(<geninj-symbol-to-stxsig> sort)
