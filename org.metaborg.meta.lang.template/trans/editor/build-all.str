module editor/build-all

imports
  signatures/aliases/-
  signatures/aterms/-
  signatures/basic/-
  signatures/characterclass/-
  signatures/constants/-
  signatures/grammar/-
  signatures/kernel/-
  signatures/labels/-
  signatures/layout/-
  signatures/layout-constraints/-
  signatures/lifting/-
  signatures/literals/-
  signatures/modules/-
  signatures/priority/-
  signatures/regular/-
  signatures/renaming/-
  signatures/restrictions/-
  signatures/sdf2-core/-
  signatures/sorts/-
  signatures/symbols/-
  signatures/TemplateLang-sig

imports
  analysis/desugar
  editor/build-utils
  editor/build-syntax
  generation/completion/-
  generation/pp/-
  generation/signatures/-
  generation/syntax/-
  generation/syntax/to-namespaced
  libstrc
  normalization/to-normal-form
  libstratego-aterm
  trans/pp
  
rules 
  check-sdf2table = prim("SSL_EXT_check_sdf2_table") <+ !"unknown"
  generate-namespaced-grammar = prim("generate_namespaced_grammar")


rules

// On-save handler
  generate-all:
    t -> ([filename1*, filename2*], [string1*, string2*])
    where
      generate-namespaced-grammar;
      (filename1*, string1*) := <generate-all-base> t;
      (filename2*, string2*) := <sdf3-to-namespaced(generate-all-namespaced(|t))> t

  generate-all:
    t -> <generate-all-base> t
    where
      not(generate-namespaced-grammar) <+
      None() := <sdf3-to-namespaced(generate-all-namespaced(|t))> t

  generate-all-namespaced(|(selected, position, _, _, project-path)):
    (namespaced-filename, namespaced-ast) -> ([namespaced-filename | filename*], [namespaced-string | string*])
    where
      namespaced-string := <pp-SDF3-string> namespaced-ast;
      (filename*, string*) := <generate-all-base> (selected, position, namespaced-ast, namespaced-filename, project-path)

  generate-all-base:
    (selected, position, ast, path, project-path) -> result
    where
      filename := <base-filename> path;
      ast'     := <desugar-templates> ast;
      <?Module(Unparameterized(mn), i*, sections)> ast
    where
      esv-cc-filename      := <get-src-gen(|project-path, "completion/colorer" ,"-cc-esv.esv")> mn;
      esv-cc-string        := <module-to-cmp-colorer; pp-esv-to-string <+ !""; debug(!"ESV files could not be generated. Try Reset and Reanalyze or check for unresolved references.\n"); fail> ast';
      esv-result           := (esv-cc-filename, esv-cc-string)
    where
      ncp-string           := <module-to-new-cmp; pp-stratego-string <+ !""; debug(!"New Completions files could not be generated. Try Reset and Reanalyze or check for unresolved references.\n"); fail> ast';
      ncp-filename         := <get-src-gen(|project-path, "completion", "-cp.str")> mn;
      ncp-result           := (ncp-filename, ncp-string)
    where
      sig-string           := <module-to-sig; pp-stratego-string <+ !""; debug(!"Signature files could not be generated. Try Reset and Reanalyze or check for unresolved references.\n"); fail> ast';
      sig-filename         := <get-src-gen(|project-path, "signatures", "-sig.str")> mn;
      sig-result           := (sig-filename, sig-string)
    where
      ds-sig-string        := <module-to-ds-sig; pp-ds-to-string  <+ !""; debug(!"The ds signature file could not be generated. Try Reset and Reanalyze or check for unresolved references.\n"); fail> ast';
      ds-sig-filename      := <get-src-gen(|project-path, "ds-signatures", "-sig.ds")> mn;
      ds-sig-result        :=  (ds-sig-filename, ds-sig-string)
    where
    version             := <check-sdf2table>;
      if <?"disabled" <+ ?"mixin-grammar"> version then
        // regular table
        norm-filename       := <get-src-gen(|project-path, "syntax/normalized",  "-norm.aterm")> mn;
        norm-ast            := <module-to-permissive; to-normal-form(|$[[project-path]/[<dirname> path]])> ast';
        norm-result         := (norm-filename, <aterm-escape-strings; pp-aterm> norm-ast);

        // completions table
        sdf-completions-filename         := <get-src-gen(|project-path, "syntax/normalized/completion", "-completion-insertions-norm.aterm")> mn;
        sdf-completions-ast              := <module-to-sdf3-completions; to-normal-form(|$[[project-path]/[<dirname> path]]) <+ !""; debug(!"SDF completion files could not be generated.\n"); fail > ast';
        sdf-completions-result           := (sdf-completions-filename, <aterm-escape-strings; pp-aterm> sdf-completions-ast);

        chars                := <collect-one(?Tokenize(<id; explode-string; un-double-quote-chars>)) <+ !['(', ')']> sections;
        newline              := <collect-one(?Newlines(<id>)) <+ !None()> sections;
        kfr                  := <collect-one(?KeywordFollowRestriction(<id; term-translation>)) <+ !None()> sections;
        kattrs               := <collect-one(?KeywordAttributes(_, _)) <+ !None()> sections;
        sdf-filename         := <get-src-gen(|project-path, "syntax", ".sdf")> mn;
        sdf-ast              := <to-sdf(|chars, newline, kfr, kattrs); pp-sdf-to-string <+ !""; debug(!"SDF files could not be generated.\n"); fail > ast';
        sdf-result           := (sdf-filename, sdf-ast);

        // to completions table from sdf2table
        sdf-completions-sdf2table-filename         := <get-src-gen(|project-path, "syntax/completion", "-completion-insertions.sdf")> mn;
        sdf-completions-sdf2table-ast              := <module-to-sdf-completions; pp-sdf-to-string  <+ !""; debug(!"SDF completion files could not be generated.\n"); fail > ast';
        sdf-completions-sdf2table-result           := (sdf-completions-sdf2table-filename, sdf-completions-sdf2table-ast);

        sdf2table-result* := [
          norm-result,
          sdf-completions-result,
          sdf-result,
          sdf-completions-sdf2table-result
        ]
      else if <?"java" <+ ?"dynamic" <+ ?"incremental"> version then
          // regular table
          norm-filename       := <get-src-gen(|project-path, "syntax/normalized",  "-norm.aterm")> mn;
          norm-ast            := <module-to-permissive; to-normal-form(|$[[project-path]/[<dirname> path]])> ast';
          norm-result         := (norm-filename, <aterm-escape-strings; pp-aterm> norm-ast);

          // completions table
          sdf-completions-filename         := <get-src-gen(|project-path, "syntax/normalized/completion", "-completion-insertions-norm.aterm")> mn;
          sdf-completions-ast              := <module-to-sdf3-completions; to-normal-form(|$[[project-path]/[<dirname> path]]) <+ !""; debug(!"SDF completion files could not be generated.\n"); fail > ast';
          sdf-completions-result           := (sdf-completions-filename, <aterm-escape-strings; pp-aterm> sdf-completions-ast)  ;

          sdf2table-result* := [
            norm-result,
            sdf-completions-result
          ]
        else
            chars                := <collect-one(?Tokenize(<id; explode-string; un-double-quote-chars>)) <+ !['(', ')']> sections;
            newline              := <collect-one(?Newlines(<id>)) <+ !None()> sections;
            kfr                  := <collect-one(?KeywordFollowRestriction(<id; term-translation>)) <+ !None()> sections;
            kattrs               := <collect-one(?KeywordAttributes(_, _)) <+ !None()> sections;
            sdf-filename         := <get-src-gen(|project-path, "syntax", ".sdf")> mn;
            sdf-ast              := <to-sdf(|chars, newline, kfr, kattrs); pp-sdf-to-string <+ !""; debug(!"SDF files could not be generated.\n"); fail > ast';
            sdf-result           := (sdf-filename, sdf-ast);

          // to completions table from sdf2table
          sdf-completions-filename         := <get-src-gen(|project-path, "syntax/completion", "-completion-insertions.sdf")> mn;
          sdf-completions-ast              := <module-to-sdf-completions; pp-sdf-to-string  <+ !""; debug(!"SDF completion files could not be generated.\n"); fail > ast';
          sdf-completions-result           := (sdf-completions-filename, sdf-completions-ast);

          sdf2table-result* := [
            sdf-result,
            sdf-completions-result
          ]
        end
      end
    where
        pp-string            := <module-to-pp; pp-stratego-string <+ !""; debug(!"PP files could not be generated. Try Reset and Reanalyze or check for unresolved references.\n"); fail> ast'
      ; pp-filename          := <get-src-gen(|project-path, "pp", "-pp.str")> mn;
        pp-result            := (pp-filename, pp-string)
    where
        result := <unzip> [
          esv-result,
          ncp-result,
          sig-result,
          ds-sig-result,
          sdf2table-result*,
          pp-result
        ]

