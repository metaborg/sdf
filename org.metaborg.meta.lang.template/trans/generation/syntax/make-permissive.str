module generation/syntax/make-permissive

imports 
  libstratego-sglr
  libspoofax/core/parse
  signatures/TemplateLang-sig
  analysis/desugar
  analysis/attributes
  analysis/lifting
  generation/gen-utils/remove-template

imports
  signatures/aliases/-
  signatures/aterms/-
  signatures/basic/-
  signatures/characterclass/-
  signatures/constants/-
  signatures/grammar/-
  signatures/kernel/-
  signatures/labels/-
  signatures/layout/-
  signatures/layout-constraints/-
  signatures/lifting/-
  signatures/literals/-
  signatures/modules/-
  signatures/priority/-
  signatures/regular/-
  signatures/renaming/-
  signatures/restrictions/-
  signatures/sdf2-core/-
  signatures/sorts/-
  signatures/symbols/-
  signatures/TemplateLang-sig

rules

  module-to-permissive-productions:
    Module(Unparameterized(mn), i*, sections*) -> Module(Unparameterized($[[mn]-permissive]), [import-main], [SDFSection(LexicalSyntax([closing-brckts-lit*, opening-brckts-lit*]))])
    where
        mn'  := <strip-annos> mn;
        rules(expanded-module-permissive: mn' -> <id>)
    where
        import-main         := Imports([Module(Unparameterized(mn))]);
        import-names*       := <collect(?Module(Unparameterized(<id>)))> i*; 
        chars               := <collect-one(?Tokenize(<id; explode-string; un-double-quote-chars>)) <+ !['(', ')']> sections*;
        sections'*          := <desugar-templates; remove-templates(|chars)> sections*;
//        imported-sections*  := <filter(expand-import-permissive(|path)); flatten-list> import-names*;
        cf-prods*           := <filter(?SDFSection(ContextFreeSyntax(<id>))); concat> [sections'*];
        closing-brckts-lit* := <filter(fetch-closing-brackets-lit); map(make-recover-insertion-prod); nub> cf-prods* ;
        opening-brckts-lit* := <filter(fetch-opening-brackets-lit); map(make-recover-insertion-prod); nub> cf-prods* 
                
  module-to-permissive:
    Module(Unparameterized(mn), i*, sections*) -> Module(Unparameterized(mn), i*, [sections*, SDFSection(LexicalSyntax([closing-brckts-lit*, opening-brckts-lit*]))])
    where
        mn'  := <strip-annos> mn
    where
        import-main         := Imports([Module(Unparameterized(mn))]);
//        import-names*       := <collect(?Module(Unparameterized(<id>)))> i*; 
        chars               := <collect-one(?Tokenize(<id; explode-string; un-double-quote-chars>)) <+ !['(', ')']> sections*;
        sections'*          := <desugar-templates; remove-templates(|chars)> sections*;
        cf-prods*           := <filter(?SDFSection(ContextFreeSyntax(<id>))); concat> [sections'*];
        closing-brckts-lit* := <filter(fetch-closing-brackets-lit); map(make-recover-insertion-prod); nub> cf-prods* ;
        opening-brckts-lit* := <filter(fetch-opening-brackets-lit); map(make-recover-insertion-prod); nub> cf-prods* 
   
  expand-import-permissive(|path):
    name -> file
    where
        name'  := <strip-annos> name;  
        <not(expanded-module-permissive)> name';
        rules(
            expanded-module-permissive:
                name' -> <id>
        );
        debug(!"name ");
        file := <resolve-path-permissive(|path); parse-file; get-sections-permissive(|path)> name;
        debug(!"sections ")        
  
  resolve-path-permissive(|path):
    name -> resolved-name
    where
        resolved-name := $[[path]/[name].sdf3]
            
  get-sections-permissive(|path):
    Module(Unparameterized(mn), i*, sections*) -> [sections'*, imported-sections'*]
    where
        import-names*      := <collect(?Module(Unparameterized(<id>)))> i*; 
        chars := <collect-one(?Tokenize(<id; explode-string; un-double-quote-chars>)) <+ !['(', ')']> sections*;
        sections'* := <desugar-templates; remove-templates(|chars)> sections*;
        imported-sections* := <filter(expand-import-permissive(|path))> import-names*;
        imported-sections'* := <desugar-templates; remove-templates(|chars)> imported-sections*
 
  //symbol shouldn't be first of any symbol in the lhs in the grammar
  fetch-closing-brackets-lit:
    SdfProductionWithCons(_, Rhs(rhs*@[h | hs]), _) -> symbol
    where
        <?Lit(_)> h;
        symbol := <last; ?Lit(_)> hs;
        <not(?"\"\"")> symbol      
        
  fetch-closing-brackets-lit:
    SdfProduction(_, Rhs(rhs*@[h | hs]), _) -> symbol
    where
        <?Lit(_)> h;
        symbol := <last; ?Lit(_)> hs;
        <not(?"\"\"")> symbol      
        
  make-recover-insertion-prod:
    l@Lit(lit) -> SdfProductionWithCons(SortCons(l, Constructor("INSERTION")), Rhs([]), Attrs([Term(Default(Fun(Unquoted("recover"))))]))
    
  
  //symbol should not be first of any other symbol in hs in the grammar?     
  fetch-opening-brackets-lit:
    SdfProductionWithCons(_, Rhs(rhs*@[l@Lit(lit) | hs]), _) -> l
    where
      <not(?"\"\"")> lit      
